plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'net.cassite'
version loadVersion()

sourceCompatibility = 17
targetCompatibility = 17

compileJava {
    doFirst {
        options.compilerArgs << '--module-path' << classpath.asPath
    }
}

compileKotlin {
    destinationDir = compileJava.destinationDir
    kotlinOptions {
        jvmTarget = '17'
    }
}

jlink {
    options = ['--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'tdpcli'
    }
    mainClass = "net.cassite.tdpcli.Main"
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.21"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.4'
    implementation 'com.github.oshi:oshi-core-java11:6.2.1'
    implementation 'org.slf4j:slf4j-nop:1.7.36'
    compileOnly 'io.vproxy:vproxy-all:MAVEN-LOCAL'
    runtimeOnly files('./vproxy-no-kt-runtime.jar')
}

def loadVersion() {
    def PREFIX = "public static final String VERSION = \""
    def SUFFIX = "\"; // _THE_VERSION_"
    def ver = file(projectDir.getAbsolutePath() + "/src/main/java/net/cassite/tdpcli/util/Version.java")
    def lines = ver.getText().split("\n")
    for (def line : lines) {
        line = line.trim()
        if (line.startsWith(PREFIX) && line.endsWith(SUFFIX)) {
            def verstr = line.substring(PREFIX.length(), line.length() - SUFFIX.length())
            if (verstr.endsWith('-DEV')) {
                verstr = verstr + '-' + versionTs + '-SNAPSHOT'
            }
            return verstr
        }
    }
    return "unknown"
}

